import java.nio.charset.StandardCharsets

plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'org.ajoberstar.reckon' version '0.16.1'
}

def generalCharset = StandardCharsets.UTF_8.name();

group = 'fr.alexpado.bots'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.options.encoding = generalCharset
compileTestJava.options.encoding = generalCharset
javadoc.options.encoding = generalCharset

reckon {
    scopeFromProp()
    stageFromProp('dev', 'beta', 'rc', 'final')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'oss'
        url 'https://s01.oss.sonatype.org/content/groups/public'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.4.4'

    implementation group: 'org.json', name: 'json', version: '20211205'
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-alpha.4'
    implementation group: 'net.htmlparser.jericho', name: 'jericho-html', version: '3.4'

    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.0.3'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'

    implementation group: 'fr.alexpado', name: 'simple-http-client', version: '1.0.2'
    implementation group: 'fr.alexpado', name: 'jda-interactions-extension', version: '0.0.7-beta.1'

    implementation group: 'io.sentry', name: 'sentry-spring-boot-starter', version: '5.6.1'
}

task copyConfigurationMetadata(type: Copy) {
    from(compileJava) {
        include 'META-INF/spring-configuration-metadata.json'
    }
    into "out/production/classes"
}

compileJava {
    dependsOn processResources
    finalizedBy copyConfigurationMetadata
}

test {
    useJUnitPlatform()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    // Example 2: disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}
