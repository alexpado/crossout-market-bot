import java.nio.charset.StandardCharsets

plugins {
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'org.ajoberstar.reckon' version '0.16.1'
}

def generalCharset = StandardCharsets.UTF_8.name()

group = 'fr.alexpado.bots'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.options.encoding = generalCharset
compileTestJava.options.encoding = generalCharset
javadoc.options.encoding = generalCharset

reckon {
    stages('dev', 'beta', 'rc', 'final')

    scopeCalc = calcScopeFromProp() | calcScopeFromCommitMessages()
    stageCalc = calcStageFromProp()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation group: 'org.flywaydb', name: 'flyway-core', version: '7.15.0'

    implementation group: 'org.json', name: 'json', version: '20220924'
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-beta.3'
    implementation group: 'net.htmlparser.jericho', name: 'jericho-html', version: '3.4'

    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.1.1'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.32'

    implementation group: 'fr.alexpado', name: 'simple-http-client', version: '1.1.1'
    implementation group: 'fr.alexpado', name: 'xodb4j', version: '1.0.0-rc.5'
    implementation(group: 'fr.alexpado', name: 'jda-interactions-extension', version: '0.1.0-rc.14') {
        exclude(group: 'io.sentry')
    }

    implementation group: 'io.sentry', name: 'sentry-spring-boot-starter', version: '6.12.1'
}

task copyConfigurationMetadata(type: Copy) {
    from(compileJava) {
        include 'META-INF/spring-configuration-metadata.json'
    }
    into "out/production/classes"
}

compileJava {
    dependsOn processResources
    finalizedBy copyConfigurationMetadata
}

test {
    useJUnitPlatform()
}

static def isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}
